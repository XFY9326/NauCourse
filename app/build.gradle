apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'bugly'

rootProject.ext {
    applicationId = 'tool.xfy9326.naucourse'
    buglyAppId = '7b78d5ccdc'
}

bugly {
    appId = rootProject.ext.buglyAppId
    appKey = 'a5a6c50e-b240-4f5c-b7bd-f1087d308bd1'
}

android {
    compileSdkVersion 30
    buildToolsVersion '30.0.2'
    ndkVersion '21.3.6528147'

    defaultConfig {
        applicationId rootProject.ext.applicationId

        minSdkVersion 21
        targetSdkVersion 30
        versionCode 83
        versionName '7.1.1'

        manifestPlaceholders = ['BUGLY_APP_ID': rootProject.ext.buglyAppId]

        resConfigs 'zh'
        applicationVariants.all {
            variant ->
                variant.outputs.all {
                    outputFileName = "${applicationId}_${versionName}_${versionCode}.apk"
                }
        }

        ndk {
            abiFilters 'armeabi-v7a', 'arm64-v8a', 'x86', 'x86_64'
        }

        javaCompileOptions {
            annotationProcessorOptions {
                arguments = ["room.schemaLocation": "$projectDir/schemas".toString()]
            }
        }
    }

    buildTypes {
        debug {
            minifyEnabled false
            manifestPlaceholders = ['DEBUG': "true"]
        }

        release {
            zipAlignEnabled true
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            manifestPlaceholders = ['DEBUG': "false"]
        }
    }

    flavorDimensions 'version'

    productFlavors {
        stable {
            dimension 'version'
            applicationId rootProject.ext.applicationId

            manifestPlaceholders = ['ApplicationId': "${applicationId}", 'BUGLY_APP_CHANNEL': "stable"]
        }
        beta {
            dimension 'version'
            applicationId rootProject.ext.applicationId

            applicationIdSuffix '.beta'
            versionNameSuffix '-beta'

            manifestPlaceholders = ['ApplicationId': "${applicationId}${applicationIdSuffix}", 'BUGLY_APP_CHANNEL': "beta"]
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = "1.8"
    }

    lintOptions {
        checkReleaseBuilds false
    }

    externalNativeBuild {
        cmake {
            path file('src/main/cpp/CMakeLists.txt')
        }
    }
}

dependencies {
    ext.kotlinx_coroutines_version = "1.3.9-native-mt"

    ext.androidx_appcompat_version = "1.2.0"
    ext.androidx_lifecycle_version = "2.2.0"
    ext.androidx_room_version = "2.2.5"

    ext.glide_version = "4.11.0"


    implementation fileTree(dir: 'libs', include: ['*.jar', '*.aar'])

    // Kotlin
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$kotlinx_coroutines_version"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$kotlinx_coroutines_version"

    // Material Design
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation 'com.google.android.material:material:1.3.0-alpha02'

    // AndroidX Core
    implementation 'androidx.core:core-ktx:1.3.1'

    // AppCompat
    implementation "androidx.appcompat:appcompat:$androidx_appcompat_version"
    implementation "androidx.appcompat:appcompat-resources:$androidx_appcompat_version"

    implementation 'androidx.preference:preference-ktx:1.1.1'
    implementation "androidx.fragment:fragment-ktx:1.2.5"

    // AndroidX View
    implementation 'androidx.gridlayout:gridlayout:1.0.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.1'

    // LifeCycle, ViewModel and LiveData
    implementation "androidx.lifecycle:lifecycle-process:$androidx_lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-common-java8:$androidx_lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$androidx_lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:$androidx_lifecycle_version"

    // Room
    implementation "androidx.room:room-runtime:$androidx_room_version"
    implementation "androidx.room:room-ktx:$androidx_room_version"
    kapt "androidx.room:room-compiler:$androidx_room_version"

    // Gson
    implementation 'com.google.code.gson:gson:2.8.6'

    // OkHttp3
    implementation 'com.squareup.okhttp3:okhttp:4.9.0'

    // Jsoup
    implementation 'org.jsoup:jsoup:1.13.1'

    // Color Picker
    implementation 'com.jaredrummler:colorpicker:1.1.0'

    // Photo View
    implementation 'com.github.chrisbanes:PhotoView:2.3.0'

    // Glide
    implementation "com.github.bumptech.glide:glide:$glide_version"
    implementation "com.github.bumptech.glide:okhttp3-integration:$glide_version"
    kapt "com.github.bumptech.glide:compiler:$glide_version"

    // Bugly
    implementation 'com.tencent.bugly:crashreport:3.2.3'

    // DB Debug
    debugImplementation 'com.amitshekhar.android:debug-db:1.0.0'
}

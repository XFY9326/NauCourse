apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'kotlin-android-extensions'

rootProject.ext {
    applicationId = 'tool.xfy9326.naucourse'
    applicationName = 'NAU课程表'
}

android {
    compileSdkVersion 29
    buildToolsVersion '29.0.2'

    defaultConfig {
        applicationId rootProject.ext.applicationId

        minSdkVersion 21
        targetSdkVersion 29
        versionCode 68
        versionName '7.0'

        resConfigs 'zh'
        applicationVariants.all {
            variant ->
                variant.outputs.all {
                    outputFileName = "${applicationId}_${versionName}_${versionCode}.apk"
                }
        }

        ndk {
            abiFilters 'armeabi-v7a', 'arm64-v8a', 'x86', 'x86_64'
        }
    }

    signingConfigs {
        release {
            storeFile KEY_STORE_FILE
            storePassword KEY_STORE_PASSWORD
            keyAlias KEY_STORE_ALIAS
            keyPassword KEY_STORE_KEY_PASSWORD
        }
    }

    buildTypes {
        debug {
            minifyEnabled false
            signingConfig signingConfigs.release
        }

        release {
            zipAlignEnabled true
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }

    flavorDimensions 'version'

    productFlavors {
        stable {
            dimension 'version'
            applicationId rootProject.ext.applicationId

            resValue('string', 'app_name', rootProject.ext.applicationName)
            manifestPlaceholders = ['ApplicationId': "${applicationId}"]
        }
        beta {
            dimension 'version'
            applicationId rootProject.ext.applicationId

            applicationIdSuffix '.beta'
            versionNameSuffix '-beta'

            resValue('string', 'app_name', "${rootProject.ext.applicationName} Beta")
            manifestPlaceholders = ['ApplicationId': "${applicationId}${applicationIdSuffix}"]
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = "1.8"
    }

    lintOptions {
        checkReleaseBuilds false
    }

    externalNativeBuild {
        cmake {
            path file('src/main/cpp/CMakeLists.txt')
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar', '*.aar'])

    // Kotlin
    implementation 'org.jetbrains.kotlin:kotlin-stdlib-jdk8:1.3.71'
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.3.5-native-mt'
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.3.5-native-mt'

    // Material Design
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation 'com.google.android.material:material:1.2.0-alpha05'

    // AndroidX Core
    implementation 'androidx.core:core-ktx:1.2.0'

    // AppCompat
    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation "androidx.appcompat:appcompat-resources:1.1.0"
    implementation 'androidx.preference:preference-ktx:1.1.0'
    implementation "androidx.fragment:fragment-ktx:1.2.4"

    // AndroidX View
    implementation 'androidx.gridlayout:gridlayout:1.0.0'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'

    // ViewModel and LiveData
    implementation 'androidx.lifecycle:lifecycle-viewmodel-ktx:2.2.0'
    implementation 'androidx.lifecycle:lifecycle-livedata-ktx:2.2.0'
    implementation 'androidx.lifecycle:lifecycle-common-java8:2.2.0'

    // Color Picker
    implementation 'com.jaredrummler:colorpicker:1.1.0'

    // Photo View
    implementation 'com.github.chrisbanes:PhotoView:2.3.0'

    // Gson
    implementation 'com.google.code.gson:gson:2.8.6'

    // OkHttp3
    implementation 'com.squareup.okhttp3:okhttp:4.5.0'

    // Jsoup
    implementation 'org.jsoup:jsoup:1.13.1'

    // Room
    implementation 'androidx.room:room-runtime:2.2.5'
    kapt 'androidx.room:room-compiler:2.2.5'
    implementation 'androidx.room:room-ktx:2.2.5'

    // DB Debug
    debugImplementation 'com.amitshekhar.android:debug-db:1.0.0'
}

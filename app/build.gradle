apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'

android {
    compileSdkVersion 30
    buildToolsVersion '30.0.3'
    ndkVersion '22.0.7026061'

    defaultConfig {
        applicationId 'tool.xfy9326.naucourse'

        minSdkVersion 21
        targetSdkVersion 30
        versionCode 87
        versionName '7.2.2'

        resConfigs 'zh'
        applicationVariants.all {
            variant ->
                variant.outputs.all {
                    outputFileName = "${applicationId}_${versionName}_${versionCode}.apk"
                }
        }

        ndk {
            abiFilters 'armeabi-v7a', 'arm64-v8a', 'x86', 'x86_64'
        }

        javaCompileOptions {
            annotationProcessorOptions {
                arguments = ["room.schemaLocation": "$projectDir/schemas".toString()]
            }
        }
    }

    buildTypes {
        release {
            zipAlignEnabled true
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            packagingOptions {
                exclude 'META-INF/*.version'
                exclude 'DebugProbesKt.bin'
                exclude 'META-INF/CHANGES'
                exclude 'META-INF/README.md'
                exclude 'okhttp3/internal/publicsuffix/NOTICE'
            }
        }
    }

    buildFeatures {
        viewBinding true
    }

    flavorDimensions 'version'

    productFlavors {
        stable {
            dimension 'version'
            applicationId 'tool.xfy9326.naucourse'

            manifestPlaceholders = ['ApplicationId': "${applicationId}"]
        }
        beta {
            dimension 'version'
            applicationId 'tool.xfy9326.naucourse'

            applicationIdSuffix '.beta'
            versionNameSuffix '-beta'

            manifestPlaceholders = ['ApplicationId': "${applicationId}${applicationIdSuffix}"]
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions.jvmTarget = JavaVersion.VERSION_1_8.toString()

    lintOptions {
        checkReleaseBuilds false
    }

    externalNativeBuild {
        cmake {
            path file('src/main/cpp/CMakeLists.txt')
        }
    }
}

dependencies {
    ext.kotlinx_coroutines_version = '1.4.2-native-mt'

    ext.androidx_appcompat_version = '1.2.0'
    ext.androidx_lifecycle_version = '2.3.0'
    ext.androidx_room_version = '2.2.6'

    ext.glide_version = '4.12.0'


    implementation fileTree(dir: 'libs', include: ['*.jar', '*.aar'])

    // Kotlin
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$kotlinx_coroutines_version"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$kotlinx_coroutines_version"

    // Material Design
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation 'com.google.android.material:material:1.3.0'

    // AndroidX Core
    implementation 'androidx.core:core-ktx:1.3.2'

    // AppCompat
    implementation "androidx.appcompat:appcompat:$androidx_appcompat_version"
    implementation "androidx.appcompat:appcompat-resources:$androidx_appcompat_version"

    implementation 'androidx.preference:preference-ktx:1.1.1'
    implementation 'androidx.fragment:fragment-ktx:1.3.0'
    implementation 'androidx.activity:activity-ktx:1.2.0'

    // AndroidX View
    implementation 'androidx.gridlayout:gridlayout:1.0.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.4'

    // LifeCycle, ViewModel and LiveData
    implementation "androidx.lifecycle:lifecycle-process:$androidx_lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-common-java8:$androidx_lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$androidx_lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:$androidx_lifecycle_version"

    // Room
    implementation "androidx.room:room-runtime:$androidx_room_version"
    implementation "androidx.room:room-ktx:$androidx_room_version"
    kapt "androidx.room:room-compiler:$androidx_room_version"

    // Gson
    implementation 'com.google.code.gson:gson:2.8.6'

    // OkHttp3
    // noinspection GradleDependency
    implementation 'com.squareup.okhttp3:okhttp:4.9.0'

    // Jsoup
    implementation 'org.jsoup:jsoup:1.13.1'

    // Color Picker
    implementation 'com.jaredrummler:colorpicker:1.1.0'

    // Photo View
    implementation 'com.github.chrisbanes:PhotoView:2.3.0'

    // Glide
    implementation "com.github.bumptech.glide:glide:$glide_version"
    implementation "com.github.bumptech.glide:okhttp3-integration:$glide_version"
    kapt "com.github.bumptech.glide:compiler:$glide_version"
}
